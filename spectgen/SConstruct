import os
env = Environment()

env.Append(LIBS = ['m','pthread', 'mpg123', 'vorbisfile', 'fftw3f', 'tag_c'])

env.Append(CPPPATH = ['decoder', 'spectgen', 'common', '../', '/usr/include/taglib'])

env.Append(CCFLAGS = ['-Wall', '-g', '-DDEBUG'])

install_programs = []
install_libs = []
prefix = '/usr'
bin_path = prefix + "/bin"
lib_path = prefix + "/lib"

DECODER_SOURCES = [
	"decoder/decoder.c",
	"decoder/decoder_backend.c",
	"decoder/decoder_backend_mpg123.c",
	"decoder/decoder_backend_vorbis.c",
	"common/queue.c",
]

DECODER_APP_SOURCES = DECODER_SOURCES + [
	"decoder/decode_app.c"
]

SPECTGEN_SOURCES = DECODER_SOURCES + [
	"spectgen/spectgen.c"
]

SPECTGEN_APP_SOURCES = SPECTGEN_SOURCES + [
	"spectgen/spectgen_app.c"
]

GENHISTDB_SOURCES = SPECTGEN_SOURCES + [
	"histdb/genhistdb.c",
	"histdb/find.c",
	"histdb/list.c",
	"histdb/hist.c",
	"histdb/hist_dist.c"
]

GENHISTDB_APP_SOURCES = GENHISTDB_SOURCES + [
	"histdb/genhistdb_app.c"
]


install_programs += env.Program('build/decoder', DECODER_APP_SOURCES)
install_programs += env.Program('build/spectgen', SPECTGEN_APP_SOURCES)
install_programs += env.Program('build/genhistdb', GENHISTDB_APP_SOURCES)

env.Alias('install', env.Install(bin_path, install_programs))
env.Alias('install', env.Install(lib_path, install_libs))
if 'uninstall' in COMMAND_LINE_TARGETS:
	env.Command("uninstall", None, Delete(FindInstalledFiles()))


